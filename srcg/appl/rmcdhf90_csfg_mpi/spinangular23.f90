!***********************************************************************
!                                                                      *
      SUBROUTINE SPINANGULAR23 (JASAV, JBSAV, IUNITF, NPOS, JCSFASAV, JCSFBSAV)
!                                                                      *
!   This routine controls the computation  and storage of the values   *
!   and all indices of the angular coefficients                        *
!                                                                      *
!                                       k                              *
!                   T  (ab)            V  (abcd)                       *
!                    rs                 rs                             *
!                                                                      *
!   k is the multipolarity of a two-particle Coulomb integral. a, b,   *
!   c and d are orbital sequence numbers.  r and s are configuration   *
!   state function indices.                                            *
!                                                                      *
!   Call(s) to: [LIB92]: ALCBUF, ALLOC, CONVRT, DALLOC, RKCO_GG,       *
!                        TNSRJJ.                                       *
!               [GENMCP]: FNDBEG, SETSDA, SORT.                        *
!                                                                      *
!   Written by Farid A. Parpia            Last revision: 28 Sep 1993   *
!   Modified by C. Froese Fischer for block computation.               *
!   Modified by G. Gaigalas and J. Bieron for new spin-angular         *
!   integration                                        01 April 2012   *
!                                                                      *
!***********************************************************************
!                                                                      *
!   Taken from mcpmpi_gg.f90                                           *
!                                                                      *
!   Modify for Spin-Angular integration: (TYPE 2 and TYPE 3 CSFGs)     *
!              <[core A] nk | H_DC | [core B] n'k' n"k">               *
!   [core] is built by labelling symmetries,                           *
!   nk, n'k', n"k" are kappa-ordered orbitals                          *
!                                                                      *
!   Written by Chongyang Chen, Fudan University, Shanghai, Oct  2023   *
!                                                                      *
!***********************************************************************
!-----------------------------------------------
!   M o d u l e s
!-----------------------------------------------
      USE vast_kind_param, ONLY:  DOUBLE
      USE parameter_def,   ONLY:  NNNW, KEYORB
      USE csfg_memory_man
!-----------------------------------------------
!   I n t e r f a c e   B l o c k s
!-----------------------------------------------
      USE alcbuf_I
      USE rkco_GG_I
!-----------------------------------------------
!   C O M M O N  B L O C K S
!-----------------------------------------------
      USE BUFFER_C,  ONLY: NVCOEF, LABEL, COEFF
      USE DEBUG_C,   ONLY: LDBPA
      USE MCP_C,     ONLY: KMAX, DIAG, LFORDR
      USE ORB_C     

      Use symmatrix_mod, KMAXTmp=>KMAX
      Use csfg_tv_C

      IMPLICIT NONE
      EXTERNAL CORD
!-----------------------------------------------
!   D u m m y   A r g u m e n t s
!-----------------------------------------------
      INTEGER  JASAV, JBSAV, IUNITF, NPOS, JA, JB, JCSFASAV, JCSFBSAV
      !INTEGER  LLISTV(0:KMAX)
      LOGICAL  LINCR
!-----------------------------------------------
!   L o c a l   P a r a m e t e r s
!-----------------------------------------------
      INTEGER, PARAMETER :: KEY = KEYORB
      INTEGER, PARAMETER :: KEYSQ = KEYORB*KEYORB
!-----------------------------------------------
!   L o c a l   V a r i a b l e s
!-----------------------------------------------
      INTEGER :: KK,IA,IB,LAB,IC,JCSFA,JCSFB,  &
         ID, NSWAP, ISWAP, LAC, LBD, NTGI
      REAL(DOUBLE), DIMENSION(NNNW) :: TSHELL
      REAL(DOUBLE) :: VCOEFF, TSHELL1, TCOEFF, TEGRAL, ENONSYM

      LOGICAL :: F0INT, LOSCAL, LRKCO, LSYM1, LSYM3
      INTEGER :: NDIFF1,NDIFF2,NORBCOLL,NORBCOLU,NORBROW
      INTEGER :: IV, I, J, K, L, M, N, IRW, ICW
      LOGICAL :: FLAGL,FLAGU
!-----------------------------------------------
!
      IF (LTRANSPOSE) THEN
        JA = JBSAV +  NCFGPAST
        JB = JASAV +  NCFGPAST
        JCSFA = JCSFBSAV
        JCSFB = JCSFASAV
      ELSE
        JA = JASAV +  NCFGPAST
        JB = JBSAV +  NCFGPAST
        JCSFA = JCSFASAV
        JCSFB = JCSFBSAV
      ENDIF

! CSFs generated by the JB-th CSFG, Row demension
      M = NTYPE(2,JB)
! CSFs generated by the JA-th CSFG, Column demension
      N = NTYPE(2,JA)
! Number of symmetry-ordered orbs of JA-CSFG
      NORBCOLL = NTYPE(4,JA) - NTYPE(3,JA) + 1
      NORBCOLU = NTYPE(6,JA) - NTYPE(5,JA) + 1
! Number of symmetry-ordered orbs of JB-CSFG 
      NORBROW = NTYPE(4,JB) - NTYPE(3,JB) + 1
!
! To use the lowest CSFs pair by default, then if the JB CSFG has one of
! the TWO symmetry-ordered orbitals of JA CSFG, the IRW and ICW meet the
! requirement that they have the same orbital by default. 
!
      IRW = IRFICT
      ICW = ICFICT
      !CALL FICTIOUS_CSF(2, IRFICT, JB, NTYPE(3,JB), NTYPE(4,JB), 0, 0)
      !CALL FICTIOUS_CSF(2, ICFICT+1, JA, NTYPE(3,JA), NTYPE(4,JA),0, 0)
      !CALL FICTIOUS_CSF(2, ICFICT+2, ICFICT+1, NTYPE(5,JA), NTYPE(6,JA), 0, 0)
      ! Copy ICFICT+2 fictious CSF to ICFICT.
      !CALL FICTIOUS_CSF(2, ICFICT, ICFICT+2, 0, 0, 0, 0)
!
! There is possible onebody contributions between TYPE 2 - 3 CSFGs.
!
      FLAGL = .FALSE.
      FLAGU = .FALSE.
      IF (NTYPE(3,JA) .EQ. NTYPE(3,JB)) THEN
        FLAGL = .TRUE.
      ELSEIF (NTYPE(5,JA) .EQ. NTYPE(3,JB)) THEN
        FLAGU = .TRUE.
      ENDIF
      IF (.NOT. (FLAGL .OR. FLAGU)) GOTO 101
!
! Onebody contributions.
!
      IA = 0
      IB = 0
      ! Read T coefficients
      IA = 0 
      CALL read_TV(1, JASAV, JBSAV, IUNITF, 1, IA, IB, TCOEFF)
      IF (IA /= 0 .AND. IA /= IB .AND.       &
             ABS (TCOEFF) > CUTOFF) THEN
! Semi-Diagonal matrixelement needed
! FLAGL: < [Core B] K | [core A] I J> /= 0 with K = I, 
! FLAGU: < [Core B] K | [core A] I J> /= 0 with K = J
        IF (IA > IB) THEN
          ISWAP = IA
          IA = IB
          IB = ISWAP
        ENDIF
        IF (IA > NORBGEN) THEN
          WRITE(*,*)'JASAV, JBSAV, IA, IB = ', &
                    JASAV, JBSAV, IA, IB
          STOP 'IA > NORBGEN .23..'
        ENDIF

        IF (LABTVFRST) NTPT = NTPT + 1
!
! TSHELL(1) should be used for the semi-diagonal matrix elements,
! for (FLAGL .AND. K == I) .OR. (FLAGU .AND. K == J). 
!           
        DO K = 1, NORBROW
          N = 0
          DO I = 1, NORBCOLL
            DO J = 1, NORBCOLU
              N = N + 1
              IF (FLAGL .AND. K == I) THEN
                IB = J + NTYPE(5,JA) - 1
                IF (LABTVFRST.OR.LCPOT) THEN
                  CALL SETCOF_NDA_CSFG(K, N, IA, IB, TCOEFF)
                ELSE
                  CALL IABINT(IA, IB, TEGRAL)
                  EMTBLOCK(K,N) = TCOEFF * TEGRAL 
                ENDIF
              ELSEIF(FLAGU .AND. K == J) THEN
                IB = I + NTYPE(3,JA) - 1
                IF (LABTVFRST.OR.LCPOT) THEN
                  CALL SETCOF_NDA_CSFG(K, N, IA, IB, TCOEFF)
                ELSE
                  CALL IABINT(IA, IB, TEGRAL)
                  EMTBLOCK(K,N) = TCOEFF * TEGRAL 
                ENDIF
              ENDIF
            ENDDO
          ENDDO
        ENDDO
      ENDIF

101   CONTINUE
!
!   Call the MCP package to generate V coefficients; ac and bd
!   are the density pairs. NVCOEF is initialized here but changed
!   in /rkco/cor[d] via COMMON.
!
      NVCOEF = 0
      CALL read_TV(2, JASAV, JBSAV, IUNITF, 1, IA, IB, TSHELL1)
      DO 2 IV = 1, NVCOEF
         VCOEFF = COEFF(IV)

! Determine the position of the symmetry-ordered orbitals
         LABV(1:5) = LABEL(1:5, IV)
         IF (LABTVFRST) NTPV = NTPV + 1
         CALL ANALABV(JASAV, JBSAV, IV)

         IF (NSYMCR .EQ. 0) THEN
           WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
                 'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
           STOP 'Warning!!! NSYMCR ERROR ***0*** 23'

         ELSEIF (NSYMCR .EQ. 1) THEN
! Replace the symmetry-ordered-orbital within LABV, the position is
! determied by IPSym(1), IPSym(2)
           IF (.NOT. (FLAGL .OR. FLAGU)) THEN
             WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')            &
                  'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
             STOP 'Warning!!! NSYMCR ERROR ***1*** 23'
           ENDIF
           N = 0
           DO I = 1, NORBCOLL
             DO J = 1, NORBCOLU
               N = N + 1
               DO K = 1, NORBROW
                 M = K
                 IF (K.NE.I .AND. K.NE.J) CYCLE
                 IF (FLAGL .AND. K == I) THEN
                   LABV(IPSym(1)) = NTYPE(5, JA) + J - 1
                   IF (LABTVFRST.OR.LCPOT) THEN
                     CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                   ELSE
                     CALL twobody_DC(TEGRAL)
                     EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                   ENDIF 
                 ELSEIF (FLAGU .AND. K == J) THEN
                   LABV(IPSym(1)) = NTYPE(3, JA) + I - 1
                   IF (LABTVFRST.OR.LCPOT) THEN
                     CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                   ELSE
                     CALL twobody_DC(TEGRAL)
                     EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                   ENDIF
                 ENDIF
               ENDDO
             ENDDO  
           ENDDO

         ELSEIF (NSYMCR .EQ. 2) THEN
           WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
                'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
           STOP 'Warning!!! NSYMCR ERROR ***2*** 23'

         ELSEIF (NSYMCR .EQ. 3) THEN
           N = 0
           DO I = 1, NORBCOLL
             LABV(IPSym(1)) = NTYPE(3, JA) + I - 1
             DO J = 1, NORBCOLU
               N = N + 1
               LABV(IPSym(2)) = NTYPE(5, JA) + J - 1
               DO K = 1, NORBROW
                 M = K
                 LABV(IPSym(3)) = NTYPE(3, JB) + K - 1
                 IF (LABTVFRST.OR.LCPOT) THEN
                   CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                 ELSE
                   CALL twobody_DC(TEGRAL)
                   EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                 ENDIF
               ENDDO
             ENDDO
           ENDDO

         ELSEIF (NSYMCR .EQ. 4) THEN
           WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
                'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
           STOP 'Warning!!! NSYMCR ERROR ***4*** 23'
         ENDIF
    2 CONTINUE

      IF (LCHM.AND.LTRANSFER) THEN
        IF (LTRANSPOSE) EMTBLOCK=TRANSPOSE(EMTBLOCK)
        CALL TRANSFER_CSFG(JASAV+NCFGPAST, JBSAV+NCFGPAST)
      ENDIF

      RETURN
      END SUBROUTINE SPINANGULAR23
