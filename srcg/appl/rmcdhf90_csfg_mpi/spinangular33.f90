!***********************************************************************
!                                                                      *
      SUBROUTINE SPINANGULAR33 (JASAV, JBSAV, IUNITF, NPOS, JCSFASAV, JCSFBSAV)
!                                                                      *
!   This routine controls the computation  and storage of the values   *
!   and all indices of the angular coefficients                        *
!                                                                      *
!                                       k                              *
!                   T  (ab)            V  (abcd)                       *
!                    rs                 rs                             *
!                                                                      *
!   k is the multipolarity of a two-particle Coulomb integral. a, b,   *
!   c and d are orbital sequence numbers.  r and s are configuration   *
!   state function indices.                                            *
!                                                                      *
!   Call(s) to: [LIB92]: ALCBUF, ALLOC, CONVRT, DALLOC, RKCO_GG,       *
!                        TNSRJJ.                                       *
!               [GENMCP]: FNDBEG, SETSDA, SORT.                        *
!                                                                      *
!   Written by Farid A. Parpia            Last revision: 28 Sep 1993   *
!   Modified by C. Froese Fischer for block computation.               *
!   Modified by G. Gaigalas and J. Bieron for new spin-angular         *
!   integration                                        01 April 2012   *
!                                                                      *
!***********************************************************************
!                                                                      *
!   Taken from mcpmpi_gg.f90                                           *
!                                                                      *
!   Modify for Spin-Angular integration: (TYPE 3 and TYPE 3 CSFGs)     *
!              <[core A] nk n'k' | H_DC | [core B] n"k" n"'k"'>        *
!   [core] is built by labelling symmetries,                           *
!   nks are kappa-ordered orbitals                                     *
!                                                                      *
!   Written by Chongyang Chen, Fudan University, Shanghai, Oct  2023   *
!                                                                      *
!***********************************************************************
!-----------------------------------------------
!   M o d u l e s
!-----------------------------------------------
      USE vast_kind_param, ONLY:  DOUBLE
      USE parameter_def,   ONLY:  NNNW, KEYORB
      USE csfg_memory_man
!-----------------------------------------------
!   I n t e r f a c e   B l o c k s
!-----------------------------------------------
      USE alcbuf_I
      USE rkco_GG_I
!-----------------------------------------------
!   C O M M O N  B L O C K S
!-----------------------------------------------
      USE BUFFER_C,  ONLY: NVCOEF, LABEL, COEFF
      USE DEBUG_C,   ONLY: LDBPA
      USE MCP_C,     ONLY: KMAX, DIAG, LFORDR
      USE ORB_C     
      Use symmatrix_mod, KMAXTmp=>KMAX
      Use csfg_tv_C

      IMPLICIT NONE
      EXTERNAL CORD
!-----------------------------------------------
!   D u m m y   A r g u m e n t s
!-----------------------------------------------
      INTEGER  JASAV, JBSAV, IUNITF, NPOS, JA, JB, JCSFASAV, JCSFBSAV
      !INTEGER  LLISTV(0:KMAX)
      LOGICAL  LINCR
!-----------------------------------------------
!   L o c a l   P a r a m e t e r s
!-----------------------------------------------
      INTEGER, PARAMETER :: KEY = KEYORB
      INTEGER, PARAMETER :: KEYSQ = KEYORB*KEYORB
!-----------------------------------------------
!   L o c a l   V a r i a b l e s
!-----------------------------------------------
      INTEGER :: KK,IA,IB,LAB,IC,JCSFA,JCSFB,  &
         ID, NSWAP, ISWAP, LAC, LBD, NTGI
      REAL(DOUBLE), DIMENSION(NNNW) :: TSHELL
      REAL(DOUBLE) :: VCOEFF, TSHELL1, TCOEFF, TEGRAL, ENONSYM

      LOGICAL :: F0INT, LOSCAL, LRKCO, FLAGLL
      LOGICAL :: FLAGLU, FLAGUL, FLAGUU, FLAGLLUU
      INTEGER :: NDIFF1,NDIFF2,NORBCOLL,NORBCOLU,NORBROWL,NORBROWU
      INTEGER :: IV, I, J, K, L, M, N, IRW, ICW, MTYPE, IORBCOL, LABTMP
      LOGICAL :: LSYM0, LSYM2, LSYM4  
!-----------------------------------------------
!
      IF (LTRANSPOSE) THEN
        JA = JBSAV +  NCFGPAST
        JB = JASAV +  NCFGPAST
        JCSFA = JCSFBSAV
        JCSFB = JCSFASAV
      ELSE
        JA = JASAV +  NCFGPAST
        JB = JBSAV +  NCFGPAST
        JCSFA = JCSFASAV
        JCSFB = JCSFBSAV
      ENDIF

! CSFs generated by the JB-th CSFG, Row demension
      M = NTYPE(2,JB)
! CSFs generated by the JA-th CSFG, Column demension
      N = NTYPE(2,JA)
! Number of symmetry-ordered orbs of JA-CSFG
      NORBCOLL = NTYPE(4,JA) - NTYPE(3,JA) + 1
      NORBCOLU = NTYPE(6,JA) - NTYPE(5,JA) + 1
! Number of symmetry-ordered orbs of JB-CSFG 
      NORBROWL = NTYPE(4,JB) - NTYPE(3,JB) + 1
      NORBROWU = NTYPE(6,JB) - NTYPE(5,JB) + 1
!
! To use the lowest CSFs pair by default, then if the JB CSFG has one of
! the TWO symmetry-ordered orbitals of JA CSFG, the IRW and ICW meet the
! requirement that they have the same orbital by default. 
!
! There might be onebody contributions between TYPE 3 - 3 CSFGs.
!
      FLAGLL   = .FALSE. 
      FLAGLU   = .FALSE.
      FLAGUL   = .FALSE.
      FLAGUU   = .FALSE.
      FLAGLLUU = .FALSE.

      IF (NTYPE(3,JB) .EQ. NTYPE(3,JA) .AND. &
          NTYPE(5,JB) .NE. NTYPE(5,JA)) FLAGLL = .TRUE.

      IF (NTYPE(3,JB) .EQ. NTYPE(5,JA)) FLAGLU = .TRUE.
      ! In such case, NTYPE(5,JB) .NE. NTYPE(3,JA)

      IF (NTYPE(5,JB) .EQ. NTYPE(3,JA)) FLAGUL = .TRUE.
      ! In such case, NTYPE(3,JB) .NE. NTYPE(5,JA)

      IF (NTYPE(3,JB) .NE. NTYPE(3,JA) .AND. &
          NTYPE(5,JB) .EQ. NTYPE(5,JA)) FLAGUU = .TRUE.

      IF (NTYPE(3,JB) .EQ. NTYPE(3,JA) .AND. &
          NTYPE(5,JB) .EQ. NTYPE(5,JA)) FLAGLLUU = .TRUE.

! There is no onebody contribution.
      IF (.NOT. FLAGLLUU) GOTO 101
!
! Onebody contributions.
!     
      ! Read T coefficients
      IA = 0
      IB = 0
      CALL read_TV(1, JASAV, JBSAV, IUNITF, 1, IA, IB, TCOEFF)
      IF (IA /= 0 .AND. ABS (TCOEFF) > CUTOFF) THEN
        ! Semi-diagonal elements, the one-body contribution arise from
        ! the labelling orbitals.
        IF (IA > NORBGEN .OR. IB > NORBGEN) THEN
          WRITE(*,*)'JASAV, JBSAV, IA, IB =', JASAV, JBSAV, IA, IB
          STOP 'IA > NORBGEN .OR. IB > NORBGEN in 33.f90 ...'
        ENDIF
        IF (IA > IB) THEN
          ISWAP = IA
          IA = IB
          IB = ISWAP 
        ENDIF
        IF (LABTVFRST) NTPT = NTPT + 1
        IF (LCHM) THEN
          CALL IABINT(IA, IB, TEGRAL)
          ENONSYM = TCOEFF * TEGRAL
        ENDIF 
        ! ENONSYM is common to all Semi-diagonal matrixelements
        IF (NTYPE(2,JB).EQ.NTYPE(2,JA)) THEN
          DO I = 1, NTYPE(2,JA)
            IF (LABTVFRST.OR.LCPOT) THEN
              CALL SETCOF_NDA_CSFG(I, I, IA, IB, TCOEFF)
            ELSE
              EMTBLOCK(I,I) = ENONSYM 
            ENDIF
          ENDDO
        ELSE
          ! NTYPE(2,JA).NE.NTYPE(2,JB). There might be more effective
          ! method to find the position of the semi-diagonal
          ! matrixelements. 
          N = 0 ! Column index
          DO I = 1, NORBCOLL
           DO J = 1, NORBCOLU
            N = N + 1
            M = 0 ! Row index
            DO K = 1, NORBROWL
             DO L = 1, NORBROWU
              M = M + 1
              IF (K.EQ.I .AND. L.EQ.J) THEN
                IF (LABTVFRST.OR.LCPOT) THEN
                  CALL SETCOF_NDA_CSFG(M, N, IA, IB, TCOEFF)
                ELSE
                  EMTBLOCK(M,N) = ENONSYM 
                ENDIF
              ENDIF
             ENDDO
            ENDDO
           ENDDO
          ENDDO
        ENDIF
      ENDIF

101   CONTINUE
!   Call the MCP package to generate V coefficients; ac and bd
!   are the density pairs. NVCOEF is initialized here but changed
!   in /rkco/cor[d] via COMMON.
!
      ENONSYM = 0.0D0
      NVCOEF = 0
      CALL read_TV(2, JASAV, JBSAV, IUNITF, 1, IA, IB, TSHELL1)
      !IF (NVCOEF > 0) &
      !  CALL PRINTLABELV(2, JA, JB, 33, 1, 0, 0, 0.0d0)
      DO 2 IV = 1, NVCOEF
        VCOEFF = COEFF(IV)

! Determine the position of the symmetry-ordered orbitals
        LABV(1:5) = LABEL(1:5, IV)
        IF (LABTVFRST) NTPV = NTPV + 1
        CALL ANALABV(JASAV, JBSAV, IV)

        IF (NSYMCR .EQ. 0) THEN
          IF (LABTVFRST.OR.LCPOT) THEN
            N = 0
            DO I = 1, NORBCOLL
              DO J = 1, NORBCOLU
                N = N + 1
                M = 0
                DO K = 1, NORBROWL
                  DO L = 1, NORBROWU
                    M = M + 1
                    IF (I == K .AND. J == L) &
                      CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                  ENDDO
                ENDDO
              ENDDO
            ENDDO
          ELSE
            CALL twobody_DC(TEGRAL)
            ENONSYM = ENONSYM + VCOEFF * TEGRAL
          ENDIF

        ELSEIF (NSYMCR .EQ. 1) THEN
          WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
               'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
          STOP 'Warning!!! NSYMCR ERROR ***1*** 33'

        ELSEIF (NSYMCR .EQ. 2) THEN
!IF (FLAGLL .AND. K == I) LNonZero(M,N) = .TRUE.
!IF (FLAGLU .AND. K == J) LNonZero(M,N) = .TRUE.
!IF (FLAGUL .AND. L == I) LNonZero(M,N) = .TRUE.
!IF (FLAGUU .AND. L == J) LNonZero(M,N) = .TRUE.
!IF (FLAGLLUU) THEN
!  IF (IORBCOL == NTYPE(3,JA) .AND. L == J) LNonZero(M,N) = .TRUE.
!  IF (IORBCOL == NTYPE(5,JA) .AND. K == I) LNonZero(M,N) = .TRUE.
!ENDIF
! Replace the symmetry-ordered-orbital within LABV, the position is
! determied by IPSym(1), IPSym(2)
          N = 0
          DO I = 1, NORBCOLL
            DO J = 1, NORBCOLU
              N = N + 1
              M = 0
              DO K = 1, NORBROWL
                DO L = 1, NORBROWU 
                  M = M + 1
                  ! Full matrix needed
                  IF (FLAGLL .AND. K.EQ.I .AND.                        &
                      LABEL(IPSYM(1),IV).EQ.NTYPE(5,JA)) THEN
                    LABV(IPSym(1)) = NTYPE(5,JA) + J - 1
                    LABV(IPSym(2)) = NTYPE(5,JB) + L - 1 
                    IF (LABTVFRST.OR.LCPOT) THEN
                      CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                    ELSE 
                      CALL twobody_DC(TEGRAL)
                      EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                    ENDIF

                  ELSEIF (FLAGLU .AND. K.EQ.J .AND.                    &
                          LABEL(IPSYM(1),IV).EQ. NTYPE(3,JA)) THEN
                    LABV(IPSym(1)) = NTYPE(3,JA) + I - 1
                    LABV(IPSym(2)) = NTYPE(5,JB) + L - 1 
                    IF (LABTVFRST.OR.LCPOT) THEN
                      CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                    ELSE 
                      CALL twobody_DC(TEGRAL)
                      EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                    ENDIF

                  ELSEIF (FLAGUL .AND. L.EQ.I .AND.                    &
                          LABEL(IPSYM(1),IV).EQ. NTYPE(5,JA)) THEN
                    LABV(IPSym(1)) = NTYPE(5,JA) + J - 1
                    LABV(IPSym(2)) = NTYPE(3,JB) + K - 1
                    IF (LABTVFRST.OR.LCPOT) THEN
                      CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                    ELSE 
                      CALL twobody_DC(TEGRAL)
                      EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                    ENDIF 

                  ELSEIF (FLAGUU .AND. L.EQ.J .AND.                    &
                          LABEL(IPSYM(1),IV).EQ. NTYPE(3,JA)) THEN
                    LABV(IPSym(1)) = NTYPE(3,JA) + I - 1
                    LABV(IPSym(2)) = NTYPE(3,JB) + K - 1 
                    IF (LABTVFRST.OR.LCPOT) THEN
                      CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                    ELSE 
                      CALL twobody_DC(TEGRAL)
                      EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                    ENDIF 

                  ELSEIF (FLAGLLUU) THEN
                    IF (K.EQ.I .AND. LABEL(IPSYM(1),IV).EQ. NTYPE(5,JA)) THEN
                      LABV(IPSym(1)) = NTYPE(5,JA) + J - 1
                      LABV(IPSym(2)) = NTYPE(5,JB) + L - 1
                      IF (LABTVFRST.OR.LCPOT) THEN
                        CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                      ELSE 
                        CALL twobody_DC(TEGRAL)
                        EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                      ENDIF
                    ENDIF
                    IF (L.EQ.J .AND. LABEL(IPSYM(1),IV).EQ. NTYPE(3,JA)) THEN
                      LABV(IPSym(1)) = NTYPE(3,JA) + I - 1
                      LABV(IPSym(2)) = NTYPE(3,JB) + K - 1
                      IF (LABTVFRST.OR.LCPOT) THEN
                        CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                      ELSE 
                        CALL twobody_DC(TEGRAL)
                        EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                      ENDIF
                    ENDIF
                  ENDIF 
                ENDDO
              ENDDO
            ENDDO  
          ENDDO

        ELSEIF (NSYMCR .EQ. 3) THEN
          WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
               'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
          STOP 'Warning!!! NSYMCR ERROR ***3*** 33'

        ELSEIF (NSYMCR .EQ. 4) THEN
          N = 0
          DO I = 1, NORBCOLL
            LABV(1) = NTYPE(3, JA) + I - 1
            DO J = 1, NORBCOLU
              N = N + 1
              LABV(2) = NTYPE(5, JA) + J - 1
              M = 0
              DO K = 1, NORBROWL
                DO L = 1, NORBROWU
                  M = M + 1
                  ! Full matrix needed
                  LABV(3) = NTYPE(3, JB) + K - 1
                  LABV(4) = NTYPE(5, JB) + L - 1
                  IF (LABEL(3,IV).GT.LABEL(4,IV)) THEN
                    LABTMP = LABV(3)
                    LABV(3) = LABV(4)
                    LABV(4) = LABTMP
                  ENDIF
                  IF (LABTVFRST.OR.LCPOT) THEN
                    CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                  ELSE 
                    CALL twobody_DC(TEGRAL)
                    EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                  ENDIF  
                ENDDO 
              ENDDO
            ENDDO
          ENDDO
        ENDIF
    2 CONTINUE
!
! Add the common parts for the diagonal matrixelement
!
      IF (LCHM.AND.DABS(ENONSYM) .GT. CUTOFF) THEN
        IF (.NOT.FLAGLLUU) THEN
          WRITE(*,*)'Unexpected .NOT.FLAGLLUU in B33.f, JA,JB=',JA,JB
          STOP 'Unexpected .NOT.FLAGLLUU in B33.f ...'
        ENDIF
        IF (NTYPE(2,JA).EQ.NTYPE(2,JB)) THEN
          DO I = 1, NTYPE(2, JA)
            EMTBLOCK(I,I) = EMTBLOCK(I,I) + ENONSYM
          ENDDO
        ELSE ! Find the semi-diagonal matrixelement
          N = 0 ! Column index
          DO I = 1, NORBCOLL
           DO J = 1, NORBCOLU
            N = N + 1
            M = 0 ! Row index
            DO K = 1, NORBROWL
             DO L = 1, NORBROWU
               M = M + 1
               IF (K.EQ.I .AND. L.EQ.J)                                &
                 EMTBLOCK(M,N) = EMTBLOCK(M,N) + ENONSYM
             ENDDO
            ENDDO
           ENDDO
          ENDDO
        ENDIF
      ENDIF

      IF (LCHM.AND.LTRANSFER) THEN
        IF (LTRANSPOSE) EMTBLOCK=TRANSPOSE(EMTBLOCK)
        CALL TRANSFER_CSFG(JASAV+NCFGPAST, JBSAV+NCFGPAST)
      ENDIF

      RETURN
      END SUBROUTINE

