!***********************************************************************
!                                                                      *
      SUBROUTINE SPINANGULAR34 (JASAV, JBSAV, IUNITF, NPOS, JCSFASAV, JCSFBSAV)
!                                                                      *
!   This routine controls the computation  and storage of the values   *
!   and all indices of the angular coefficients                        *
!                                                                      *
!                                       k                              *
!                   T  (ab)            V  (abcd)                       *
!                    rs                 rs                             *
!                                                                      *
!   k is the multipolarity of a two-particle Coulomb integral. a, b,   *
!   c and d are orbital sequence numbers.  r and s are configuration   *
!   state function indices.                                            *
!                                                                      *
!   Call(s) to: [LIB92]: ALCBUF, ALLOC, CONVRT, DALLOC, RKCO_GG,       *
!                        TNSRJJ.                                       *
!               [GENMCP]: FNDBEG, SETSDA, SORT.                        *
!                                                                      *
!   Written by Farid A. Parpia            Last revision: 28 Sep 1993   *
!   Modified by C. Froese Fischer for block computation.               *
!   Modified by G. Gaigalas and J. Bieron for new spin-angular         *
!   integration                                        01 April 2012   *
!                                                                      *
!***********************************************************************
!                                                                      *
!   Taken from mcpmpi_gg.f90                                           *
!                                                                      *
!   Modify for Spin-Angular integration: (TYPE 3 and TYPE 4 CSFGs)     *
!              <[core A] nk n'k' | H_DC | [core B] (n"-1)k" n"k">      *
!   [core] is built by labelling symmetries,                           *
!   nks are kappa-ordered orbitals                                     *
!                                                                      *
!   Written by Chongyang Chen, Fudan University, Shanghai, Oct  2023   *
!                                                                      *
!***********************************************************************
!-----------------------------------------------
!   M o d u l e s
!-----------------------------------------------
      USE vast_kind_param, ONLY:  DOUBLE
      USE parameter_def,   ONLY:  NNNW, KEYORB
      USE csfg_memory_man
!-----------------------------------------------
!   I n t e r f a c e   B l o c k s
!-----------------------------------------------
      USE alcbuf_I
      USE rkco_GG_I
!-----------------------------------------------
!   C O M M O N  B L O C K S
!-----------------------------------------------
      USE BUFFER_C,  ONLY: NVCOEF, LABEL, COEFF
      USE DEBUG_C,   ONLY: LDBPA
      USE MCP_C,     ONLY: KMAX, DIAG, LFORDR
      USE ORB_C     

      Use symmatrix_mod, KMAXTmp=>KMAX
      Use csfg_tv_C

      IMPLICIT NONE
      EXTERNAL CORD
!-----------------------------------------------
!   D u m m y   A r g u m e n t s
!-----------------------------------------------
      INTEGER  JASAV, JBSAV, IUNITF, NPOS, JA, JB, JCSFASAV, JCSFBSAV
      !INTEGER  LLISTV(0:KMAX)
      LOGICAL  LINCR
!-----------------------------------------------
!   L o c a l   P a r a m e t e r s
!-----------------------------------------------
      INTEGER, PARAMETER :: KEY = KEYORB
      INTEGER, PARAMETER :: KEYSQ = KEYORB*KEYORB
!-----------------------------------------------
!   L o c a l   V a r i a b l e s
!-----------------------------------------------
      INTEGER :: KK,IA,IB,LAB,IC,JCSFA,JCSFB,  &
         ID, NSWAP, ISWAP, LAC, LBD, NTGI
      REAL(DOUBLE), DIMENSION(NNNW) :: TSHELL
      REAL(DOUBLE) :: VCOEFF, TSHELL1, TCOEFF, TEGRAL, ENONSYM

      LOGICAL :: F0INT, LOSCAL, LRKCO, FLAGL, FLAGU, LSYM2, LSYM4
      INTEGER :: NDIFF1,NDIFF2,NORBCOL,NORBROWL,NORBROWU
      INTEGER :: IV, I, J, K, L, M, N, IRW, ICW, MTYPE, LABTMP
!-----------------------------------------------
!
      IF (LTRANSPOSE) THEN
        JA = JBSAV +  NCFGPAST
        JB = JASAV +  NCFGPAST
        JCSFA = JCSFBSAV
        JCSFB = JCSFASAV
      ELSE
        JA = JASAV +  NCFGPAST
        JB = JBSAV +  NCFGPAST
        JCSFA = JCSFASAV
        JCSFB = JCSFBSAV
      ENDIF

! CSFs generated by the JB-th CSFG, Row demension
      M = NTYPE(2,JB)
! CSFs generated by the JA-th CSFG, Column demension
      N = NTYPE(2,JA)
! Number of symmetry-ordered orbs of JA-CSFG
      NORBCOL = NTYPE(6,JA) - NTYPE(3,JA) + 1
! Number of symmetry-ordered orbs of JB-CSFG 
      NORBROWL = NTYPE(4,JB) - NTYPE(3,JB) + 1
      NORBROWU = NTYPE(6,JB) - NTYPE(5,JB) + 1
!
! To use the lowest CSFs pair by default, then if the JB CSFG has one of
! the TWO symmetry-ordered orbitals of JA CSFG, the IRW and ICW meet the
! requirement that they have the same orbital by default. 
!
      !IRW = IRFICT
      !ICW = ICFICT
      ! | IR [CoreB] 7s 7p> | ==> | IR [CoreB] 4s 4p>
      ! | IR [CoreB] 7p 7d> | ==> | IR [CoreB] 4p 4d>
      !CALL FICTIOUS_CSF(2, IRFICT+1, JB, NTYPE(3,JB), NTYPE(4,JB), &
      !                                   0        ,           0)
      !CALL FICTIOUS_CSF(2, IRFICT+2, IRFICT+1, NTYPE(5,JB), NTYPE(6,JB), &
      !                                   0        ,           0)
      !CALL FICTIOUS_CSF(2, IRFICT, IRFICT+2, 0, 0, 0, 0)

      ! < IC [CoreA] 6p 7p | ==> < IC [CoreA] 4p 5p |
      !CALL FICTIOUS_CSF(4, ICFICT, JA, NTYPE(3,JA), NTYPE(4,JA), &
      !                                 NTYPE(5,JA), NTYPE(6,JA))
      FLAGL = .FALSE.
      FLAGU = .FALSE.
      IF (NTYPE(3,JB) == NTYPE(3,JA)) THEN
        FLAGL = .TRUE.
!
! <IRFICT   K L | h12 | ICFICT I J>: K = I
! 
!
! <IRFICT+1 K L | h12 | ICFICT I J>: K = J
! 
        IF (NORBROWL >= 2) THEN
      ! | IR [CoreB] 7p 7d> | ==> | IR [CoreB] 5p 4d>
          !CALL FICTIOUS_CSF(2, IRFICT+1, IRFICT, NTYPE(3,JB)+1, &
          !                                       NTYPE(3,JB), 0, 0)
        ENDIF
      ELSEIF (NTYPE(5,JB) == NTYPE(3,JA)) THEN
!
! <IRFICT   K L | h12 | ICFICT I J>: L = I
! 
!
! <IRFICT+1 K L | h12 | ICFICT I J>: L = J
! 
        FLAGU = .TRUE.
        IF (NORBROWU >= 2) THEN
      ! | IR [CoreB] 7s 7p> | ==> | IR [CoreB] 4s 5p>
          !CALL FICTIOUS_CSF(2, IRFICT+1, IRFICT, NTYPE(5,JB)+1, &
          !                                       NTYPE(5,JB), 0, 0)
        ENDIF
      ENDIF
!
! No onebody contributions.
! There is no onebody contributions between TYPE 3 - 4 CSFGs.
!
!   Call the MCP package to generate V coefficients; ac and bd
!   are the density pairs. NVCOEF is initialized here but changed
!   in /rkco/cor[d] via COMMON.
!
      DO MTYPE = 1, 2
        IF (MTYPE == 1) THEN
          IRW = IRFICT
          ICW = ICFICT
        ELSE
          IF (.NOT. (FLAGL .OR. FLAGU)) CYCLE
          IF (FLAGL .AND. NORBROWL == 1) CYCLE
          IF (FLAGU .AND. NORBROWU == 1) CYCLE
          IRW = IRFICT + 1
          ICW = ICFICT
        ENDIF     
        NVCOEF = 0
        CALL read_TV(2, JASAV, JBSAV, IUNITF, MTYPE, IA, IB, TSHELL1)
        DO 2 IV = 1, NVCOEF
          VCOEFF = COEFF(IV)

! Determine the position of the symmetry-ordered orbitals
          LABV(1:5) = LABEL(1:5, IV)
          IF (LABTVFRST) NTPV = NTPV + 1
          CALL ANALABV(JASAV, JBSAV, IV)

          IF (NSYMCR .EQ. 0) THEN
            WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
                 'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
            STOP 'Warning!!! NSYMCR ERROR ***0*** 34'

          ELSEIF (NSYMCR .EQ. 1) THEN
            WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
                 'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
            STOP 'Warning!!! NSYMCR ERROR ***1*** 34'

          ELSEIF (NSYMCR .EQ. 2) THEN
            N = 0
            DO I = 1, NORBCOL - 1
              DO J = I + 1, NORBCOL
                N = N + 1
                M = 0
                DO K = 1, NORBROWL
                  DO L = 1, NORBROWU
                    M = M + 1
                    ! full matrix needed
              IF (MTYPE == 1) THEN
!IF (FLAGL .AND. MTYPE == 1 .AND. K == I ) &
                IF (FLAGL .AND. K == I) THEN
                  LABV(IPSYM(1)) = NTYPE(3, JA) + J - 1
                  LABV(IPSYM(2)) = NTYPE(5, JB) + L - 1
                  IF (LABTVFRST.OR.LCPOT) THEN
                    CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                  ELSE 
                    CALL twobody_DC(TEGRAL)
                    EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                  ENDIF
                ENDIF
!IF (FLAGU .AND. MTYPE == 1 .AND. L == I ) &
                IF (FLAGU .AND. L == I) THEN
                  LABV(IPSYM(1)) = NTYPE(3, JA) + J - 1
                  LABV(IPSYM(2)) = NTYPE(3, JB) + K - 1
                  IF (LABTVFRST.OR.LCPOT) THEN
                    CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                  ELSE 
                    CALL twobody_DC(TEGRAL)
                    EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                  ENDIF
                ENDIF

              ELSEIF (MTYPE == 2) THEN
!IF (FLAGL .AND. MTYPE == 2 .AND. K == J ) &
                IF (FLAGL .AND. K == J) THEN
                  LABV(IPSYM(1)) = NTYPE(3, JA) + I - 1
                  LABV(IPSYM(2)) = NTYPE(5, JB) + L - 1
                  IF (LABTVFRST.OR.LCPOT) THEN
                    CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                  ELSE 
                    CALL twobody_DC(TEGRAL)
                    EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                  ENDIF 
                ENDIF
!IF (FLAGU .AND. MTYPE == 2 .AND. L == J ) &
                IF (FLAGU .AND. L == J) THEN
                  LABV(IPSYM(1)) = NTYPE(3, JA) + I - 1
                  LABV(IPSYM(2)) = NTYPE(3, JB) + K - 1
                  IF (LABTVFRST.OR.LCPOT) THEN
                    CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                  ELSE
                    CALL twobody_DC(TEGRAL)
                    EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                  ENDIF 
                ENDIF
              ENDIF
                  ENDDO
                ENDDO
              ENDDO  
            ENDDO

          ELSEIF (NSYMCR .EQ. 3) THEN
            WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
                 'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
            STOP 'Warning!!! NSYMCR ERROR ***3*** 34'

          ELSEIF (NSYMCR .EQ. 4) THEN
            IF (MTYPE /= 1) THEN
            ! In rangular_mpi_csfg: savetv.f90, V-Coefficients with 
            ! NSYMCR = 4 and MTYPE = 2 are discarded.
              WRITE(*,'(a3, i7, 2x, a3, i7, 2x, a5, 5i3)')              &
                   'IC=', JASAV, 'IR=', JBSAV, 'LABV=', LABV(1:5)
              STOP 'Warning!!! NSYMCR ERROR ***4*** 34'
            ENDIF
            N = 0
            DO I = 1, NORBCOL - 1
              LABV(1) = NTYPE(3, JA) + I - 1
              DO J = I + 1, NORBCOL
                N = N + 1
                LABV(2) = NTYPE(3, JA) + J - 1
                M = 0
                DO K = 1, NORBROWL
                  DO L = 1, NORBROWU
                    M = M + 1
                    ! Full matrix needed
                    LABV(3) = NTYPE(3, JB) + K - 1
                    LABV(4) = NTYPE(5, JB) + L - 1
                    IF (LABEL(3,IV).GT.LABEL(4,IV)) THEN
                      LABTMP = LABV(3)
                      LABV(3) = LABV(4)
                      LABV(4) = LABTMP
                    ENDIF
                    IF (LABTVFRST.OR.LCPOT) THEN
                      CALL SETCOF_NXY_CSFG(M, N, VCOEFF)
                    ELSE
                      CALL twobody_DC(TEGRAL)
                      EMTBLOCK(M,N) = EMTBLOCK(M,N) + VCOEFF * TEGRAL
                    ENDIF 
                  ENDDO 
                ENDDO
              ENDDO
            ENDDO
          ENDIF
    2   CONTINUE
      ENDDO

      IF (LCHM.AND.LTRANSFER) THEN
        IF (LTRANSPOSE) EMTBLOCK=TRANSPOSE(EMTBLOCK)
        CALL TRANSFER_CSFG(JASAV+NCFGPAST, JBSAV+NCFGPAST)
      ENDIF

      RETURN
      END SUBROUTINE
